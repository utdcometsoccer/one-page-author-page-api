customEvents
| where name == 'AuthorLoadEvent'
| extend AuthorDomainName = tostring(todynamic(customDimensions)["domain"])
| extend NormalizedDomain = iff((AuthorDomainName startswith "www."), substring(AuthorDomainName, 4), AuthorDomainName)
| where NormalizedDomain != "localhost" 
    and NormalizedDomain !~ "127.0.0.1"
    and not (NormalizedDomain matches regex @"^\d{1,3}\.\d{1,3}\.\d{1,3}\.\d{1,3}$")
    and not (NormalizedDomain matches regex @"^::1$|^[0-9a-fA-F:]+$")
| summarize DomainNameRequest = count() by NormalizedDomain, bin(timestamp, 1h)
| render areachart
